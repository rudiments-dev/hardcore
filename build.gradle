buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
}

def base_version = '0.5-SNAPSHOT'

allprojects {
    group 'dev.rudiments'
    version base_version

    repositories {
        mavenLocal()
        mavenCentral()
    }
}

def scalaModules() {
    subprojects.findAll { new File(it.projectDir, 'src/main/scala').directory }
}

configure(scalaModules()) {
    apply plugin: 'java'
    apply plugin: 'scala'

    sourceCompatibility = 17
    targetCompatibility = 17

    test {
        reports {
            html.required = true
            junitXml.required = true
        }
        maxHeapSize = "2048m"
        testLogging {
            events "skipped", "failed"
            exceptionFormat "full"
        }
    }

    dependencies {
        implementation 'org.scala-lang:scala3-library_3:3.1.3'

        implementation 'org.slf4j:slf4j-api:2.0.6'

        testImplementation 'org.scalatest:scalatest_3:3.2.15'
        testImplementation 'org.scalatestplus:junit-4-13_3:3.2.15.0'

        testImplementation 'junit:junit:4.13.2'
        testImplementation 'ch.qos.logback:logback-classic:1.4.5'
    }
}